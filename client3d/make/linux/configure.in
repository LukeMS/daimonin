AC_PREREQ(2.59)

AC_INIT([Daimonin_3D_Client], 0.0.1)
AC_CONFIG_AUX_DIR(make_utils)
AM_INIT_AUTOMAKE([Daimonin_3D_Client], 0.0.1)
#AM_CONFIG_HEADER(../../source/include/config.h)

AC_PREFIX_DEFAULT(./../../../../client-3d-alpha1)
AC_CONFIG_SRCDIR(../../source/main.cpp)

AC_PROG_CC
AM_PROG_CC_C_O
AC_PROG_CXX
AC_PROG_MAKE_SET
AC_PROG_INSTALL

dnl find the ogre package
export PKG_CONFIG_PATH=/usr/lib/pkgconfig:/usr/local/lib/pkgconfig

PKG_CHECK_MODULES(OGRE, OGRE)
AC_SUBST(OGRE_CFLAGS)
AC_SUBST(OGRE_LIBS)
AC_SUBST(OGRE_PLUGINDIR, `pkg-config --variable=plugindir OGRE`)

dnl Find the Object-oriented input system
PKG_CHECK_MODULES(OIS, OIS)
AC_SUBST(OIS_CFLAGS)
AC_SUBST(OIS_LIBS)

# Find tinyxml stuff
#AC_SUBST(TXML_CFLAGS, "-I3rdParty/tinyxml")

# Find zlib stuff
#AC_SUBST(ZLIB_CFLAGS, "-I3rdParty/zlib")

# Test CPU & compiler characteristics
AC_C_BIGENDIAN(
    [endianness="big"]
	[AC_DEFINE(WORDS_BIGENDIAN, 1, [ CPU uses big-endian words ])],
    [endianness="little"]
	[AC_DEFINE(WORDS_LITTLEENDIAN, 1, [ CPU uses little-endian words ])],
    [endianness="unknown"])
AC_CHECK_SIZEOF(int *)
cpubits=`expr $ac_cv_sizeof_int_p \* 8`

# Select correct fmod library
#AC_SUBST(FMOD_CFLAGS, "-I3rdParty/fmod")
if test "x$cpubits" = "x64"; then
    AC_SUBST(FMOD_LIBS, "-lfmodex64")
else
    AC_SUBST(FMOD_LIBS, "-lfmodex")
fi

AC_PATH_PROG(DEPEND, makedepend)
AC_PATH_PROG(CP,cp)
AC_PATH_PROG(RM,rm)
AC_PATH_PROG(RMDIR,rmdir)
AC_PATH_PROG(MKDIR,mkdir)

AC_CHECK_FUNCS(stricmp strnicmp strcasecmp strncasecmp)
AC_CHECK_HEADERS([unistd.h])
AC_CHECK_HEADERS([fcntl.h])

if test "x$prefix" != xNONE; then
   AC_DEFINE_UNQUOTED(PREFIX, "${prefix}", [Installation prefix])
else
   AC_DEFINE_UNQUOTED(PREFIX, "${ac_default_prefix}", [Installation prefix])
fi

#
# SDL_CONFIG ("borrowed" from Wesnoth 0.7.3)
#

AC_PATH_PROGS([SDL_CONFIG], [sdl-config sdl11-config], [none])
if test "x$SDL_CONFIG" = "xnone"; then
	AC_MSG_ERROR([*** SDL not found! Get SDL from www.libsdl.org.
If you already installed it, check it's in the path. If problem remains,
please send a mail to the address that appears in ./configure --version
indicating your platform, the version of configure script and the problem.])
fi

# SDL Libs

SDL_LIBS=`$SDL_CONFIG --libs`
AC_SUBST([SDL_LIBS])

# Check for SDL headers

OLD_CPPFLAGS=$CPPFLAGS
OLD_CXXFLAGS=$CXXFLAGS

SDL_CFLAGS=`$SDL_CONFIG --cflags`
SDL_CFLAGS="$SDL_CFLAGS"
CPPFLAGS="$CPPFLAGS $SDL_CFLAGS"
CXXFLAGS="$CXXFLAGS $SDL_CFLAGS"

AC_CHECK_HEADER([SDL.h],
		[],
		[AC_MSG_ERROR([*** SDL include files not found!
You should install SDL development package.])])

CPPFLAGS=$OLD_CPPFLAGS
CXXFLAGS=$OLD_CXXFLAGS

AC_SUBST([SDL_CFLAGS])


AC_OUTPUT(Makefile ../../plugins.cfg)
#AC_OUTPUT(Makefile ../../source/Makefile ../../include/Makefile ../../3rdParty/fmod/Makefile ../../3rdParty/tinyxml/Makefile ../../3rdParty/zlib/Makefile)
