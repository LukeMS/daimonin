AC_INIT([Daimonin SDL Client], [BETA3-0.966], [support@daimonin.sourceforge.net])

AC_CONFIG_AUX_DIR(make_utils)
AC_CANONICAL_SYSTEM

AC_PREFIX_DEFAULT(../../../client-BETA3-0.966)

AC_PROG_CC
AC_PROG_MAKE_SET
AC_PROG_INSTALL

AC_ARG_ENABLE(debug,
   [  --enable-debug          compile in debug messages],
   [export USE_DEBUG=yes],
   [export USE_DEBUG=no]
)

AC_ARG_ENABLE(sound,
   [  --enable-sound          compile with SDL sound (default)],
   [ if test "x$enableval" = "xyes" ; then
		AC_DEFINE(INSTALL_SOUND,, [ Use the SDL_mixer sound system. Remove when you have no sound card or slow computer])
	 fi
   ],
   [AC_DEFINE(INSTALL_SOUND,, [ Use the SDL_mixer sound system. Remove when you have no sound card or slow computer])]
)

AC_ARG_ENABLE(opengl,
   [  --enable-opengl         compile with OpenGL support (experimental)],
   [  if test "x$enableval" = "xyes" ; then
	     AC_DEFINE(INSTALL_OPENGL,, [Use OpenGL to do the render/blits (experimental)])
	  fi
   ]
)

AC_ARG_ENABLE(win32,
   [  --enable-win32          compile with Win32 support],
   [  if test "x$enableval" = "xyes" ; then
		 AC_DEFINE(__WIN_32, 1, [Win32])
      else
		 AC_DEFINE(__LINUX, 1, [Linux])
      fi
   ],
   [ AC_DEFINE(__LINUX, 1, [Linux]) ]
)

AC_PATH_PROG(DEPEND, makedepend)
AC_PATH_PROG(CP,cp)
AC_PATH_PROG(RM,rm)
AC_PATH_PROG(RMDIR,rmdir)
AC_PATH_PROG(MKDIR,mkdir)
AC_PATH_PROG([WGET],wget, [none])
if test "x$WGET" = "xnone"; then
	AC_MSG_ERROR([*** wget not found! *** 
wget is used by the auto-updater.
Install it first please!])
fi
AC_PATH_PROG([MD5SUM],md5sum, [none])
if test "x$MD5SUM" = "xnone"; then
	AC_MSG_ERROR([*** md5sum not found! *** 
md5sum is used by the auto-updater.
Install it first please!])
fi
AC_PATH_PROG([TAR],tar, [none])
if test "x$TAR" = "xnone"; then
	AC_MSG_ERROR([*** tar not found! *** 
tar is used by the auto-updater.
Install it first please!])
fi
AC_PATH_PROG([BUNZIP2],bunzip2, [none])
if test "x$BUNZIP2" = "xnone"; then
	AC_MSG_ERROR([*** bunzip2 not found! *** 
bunzip2 is used by the auto-updater.
Install it first please!])
fi

AC_PATH_PROGS([SDL_CONFIG], [sdl-config sdl11-config], [none])
if test "x$SDL_CONFIG" = "xnone"; then
	AC_MSG_ERROR([*** SDL not found! Get SDL from www.libsdl.org.
If you already installed it, check it's in the path. If problem remains,
please send a mail to the address that appears in ./configure --version
indicating your platform, the version of configure script and the problem.])
fi

AC_SUBST(SDL_CFLAGS, `${SDL_CONFIG} --cflags`)
AC_SUBST(SDL_LIBS, `${SDL_CONFIG} --libs`)

AC_CHECK_FUNCS(stricmp strnicmp strcasecmp strncasecmp getaddrinfo)
AC_CHECK_HEADERS([unistd.h])
AC_CHECK_HEADERS([fcntl.h])

if test "x$prefix" != xNONE; then
   AC_DEFINE_UNQUOTED(PREFIX, "${prefix}", [Installation prefix])
else
   AC_DEFINE_UNQUOTED(PREFIX, "${ac_default_prefix}", [Installation prefix])
fi

AC_CONFIG_HEADER(../../src/include/define.h)
AC_OUTPUT(Makefile ../../src/updater/Makefile ../../src/Makefile)
